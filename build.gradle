buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'

def ENV = System.getenv()
if (ENV.BUILD_NUMBER) {
    version = "1.1." + "${System.getenv().BUILD_NUMBER}"
} else {
    version = "1.1.BUILD"
}

minecraft {
    version = "1.7.10-10.13.0.1272"
    replace "@MODVERSION@", project.version
    replaceIn "Core.java"
}

group = 'ModmussCore'

task deleteOldArtifacts(type: Delete) {
    delete 'build/libs'
}

classes.dependsOn deleteOldArtifacts


processResources
        {
            // this will ensure that this task is redone when the versions change.
            inputs.property "version", project.version
            inputs.property "mcversion", project.minecraft.version

            // replace stuff in mcmod.info, nothing else
            from(sourceSets.main.resources.srcDirs) {
                include '*.info'

                // replace version and mcversion
                expand 'version':project.version, 'mcversion':project.minecraft.version
            }

            // copy everything else, thats not the mcmod.info
            from(sourceSets.main.resources.srcDirs) {
                exclude '**/*.info'
            }
        }

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    appendix = 'src'
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    appendix = 'dev'
    exclude "**/*.psd"
}

jar {
    manifest {
        attributes (['FMLCorePlugin': 'me.modmuss50.network.FmlLoadingCore', 'FMLCorePluginContainsFMLMod': 'true'])
    }
    exclude "**/*.psd"
    appendix = 'mod'
}

artifacts {
    archives sourceJar
    archives deobfJar
}